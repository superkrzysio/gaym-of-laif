HAI "This iz teh test"

  O HAI IM TehRandomness
    I HAS A multiplier ITZ 1337
    I HAS A modulo ITZ 2137
    I HAS A x0 ITZ 999

    HOW IZ I absolute YR num
      BOTH SAEM 0 AN BIGGR OF 0 AN num, O RLY?
      YA RLY
        FOUND YR PRODUKT OF num AN -1
      NO WAI
        FOUND YR num
      OIC
    IF U SAY SO

    HOW IZ I generating YR min AN YR max
      BTW r * X * (1 - X)
      ME'Z x0 R MOD OF ...
        PRODUKT OF ...
          ME'Z multiplier ...
          AN PRODUKT OF ME'Z x0 ...
            AN DIFF OF 1 AN ME'Z x0 ...
        AN ME'Z modulo
      
      ME'Z x0 R ME IZ absolute YR ME'Z x0 MKAY

      FOUND YR SUM OF ...
        MOD OF ...
          ME'Z x0 ...
          AN DIFF OF max AN min ...
        AN min
      
    IF U SAY SO

  KTHX

  BTW, testing here

  I HAS A rezult ITZ ""
  IM IN YR tesTLoopie UPPIN YR x TIL BOTH SAEM x AN 100
    rezult R SMOOSH rezult AN " " AN TehRandomness IZ generating YR 2 AN YR 10 MKAY MKAY
  IM OUTTA YR tesTLoopie

  VISIBLE rezult
  BTW, shood B random

KTHXBYE